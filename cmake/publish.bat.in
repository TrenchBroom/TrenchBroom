@ECHO OFF

IF "%~1"=="Alpha" GOTO dobuild
IF "%~1"=="Beta" GOTO dobuild
IF "%~1"=="Stable" GOTO dobuild
GOTO argsfailed

:dobuild

IF "%~1"=="Stable" (
	SET CONFIG="Release"
) else (
	SET CONFIG="RelWithDebInfo"
)

cmake -G "@CMAKE_GENERATOR@" "@CMAKE_SOURCE_DIR@" -DAPP_BUILD_CHANNEL=%1 -DCMAKE_BUILD_TYPE=%CONFIG%
cmake --build . --config %CONFIG% -- %2 %3 %4 %5 %6 %7
IF ERRORLEVEL 1 GOTO buildfailed

%CONFIG%\TrenchBroom-Test
IF ERRORLEVEL 1 GOTO testfailed

cmake --build . --config %CONFIG% --target package -- %2 %3 %4 %5 %6 %7
IF ERRORLEVEL 1 GOTO packagefailed
CALL upload.bat %1

GOTO end

:argsfailed
ECHO You must supply the build channel.
ECHO Example: publish.bat channel
ECHO   where channel is 'Alpha', 'Beta', or 'Stable'
EXIT /B 1
GOTO end

:buildfailed
ECHO Build failed, aborting.
EXIT /B 1
GOTO end

:testfailed
cd ..
ECHO Unit tests failed, aborting.
EXIT /B 1
GOTO end

:packagefailed
ECHO Packaging failed, aborting.
EXIT /B 1
GOTO end

:end
